apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'gwt'
apply plugin: 'jetty'
apply plugin: 'eclipse'


group = 'fr.putnami.pwt'
version = '1.1.0'

sourceCompatibility = 1.7
targetCompatibility = 1.7

buildscript {
	repositories {
		jcenter()
	}
	dependencies {
		classpath 'de.richsource.gradle.plugins:gwt-gradle-plugin:0.5'
	}
}

ext {
	pwtVersion = '1.1.1'
}
	
configurations {
	log4j
}

repositories {
	mavenLocal()
	mavenCentral()
}

dependencies {	
	log4j 'org.slf4j:slf4j-api:1.6.1'
	log4j 'org.slf4j:jcl-over-slf4j:1.6.1'
	log4j 'org.slf4j:slf4j-log4j12:1.6.1'
	log4j 'log4j:log4j:1.2.16'

	runtime configurations.log4j	

	compile "fr.putnami.pwt:pwt:$pwtVersion"
	compile "javax.validation:validation-api:1.0.0.GA"
	
	testCompile 'junit:junit:4.11'
	
}

gwt {
	gwtVersion='2.6.1'

	gwtDev {
		jvmArgs('-Xdebug',  '-Xrunjdwp:transport=dt_socket,server=y,suspend=n,address=8000')
	}
	
	modules = ['fr.putnami.pwt.sample.web.SampleWeb']
	devModules = ['fr.putnami.pwt.sample.web.SampleWebDev']

	superDev {
		noPrecompile=true
	}
	
	// The following is only needed if you don't use the Google Plugin for Eclipse.
	eclipse{
		addGwtContainer=false // Default set to true
	}
}

//Tweak to move the GWT container classpath entry to the end
// Must be defined after the gwt-plugin configuration
if (gwt.eclipse.addGwtContainer) {
	eclipse.classpath.file{
		whenMerged{ classpath ->
			def projectRefs = classpath.entries.findAll{entry -> entry.kind =='con' && entry.path.equals('com.google.gwt.eclipse.core.GWT_CONTAINER')}
			//move the project references to the end of the list:
			classpath.entries.removeAll(projectRefs)
			classpath.entries.addAll(projectRefs)
		}
	}
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.web
        }
    }
}

eclipse {
	classpath {
		downloadSources=true
	}
}

/**
*	Use of Jetty plugin with draft GWT compilation
*/
task jettyDraftWar(type: JettyRunWar) {
	dependsOn draftWar
	dependsOn.remove('war')
	webApp=draftWar.archivePath
}

